configurations {
    war
}

dependencies {
    compile project(':tomcat-embedded-launcher')

    runtime("org.alfresco:alfresco-repository:$alfrescoVersion:h2scripts") {
        transitive = false
    }
    runtime 'com.h2database:h2:1.4.192',
            rootProject,
            files("$buildDir/WEB-INF/classes"),
            fileTree(dir: "$buildDir/WEB-INF/lib", include: '*.jar')

    war "org.alfresco:alfresco:$alfrescoVersion:@war"
}

def webapp = "$buildDir/webapps/alfresco"

task extractWar(type: Copy) {
    from {
        configurations.war.collect { zipTree(it) }
    }
    into webapp
    eachFile { details ->
        details.path = details.path.replaceFirst('WEB-INF/classes', '../../WEB-INF/classes')
        details.path = details.path.replaceFirst('WEB-INF/lib', '../../WEB-INF/lib')
    }
}

task startTomcat(type: JavaExec, dependsOn: extractWar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'xyz.its_me.alfresco.Launcher'
    systemProperties = ['tomcat.port'                                      : '8080',
                        'tomcat.context-path'                              : '/alfresco',
                        'tomcat.doc-base'                                  : webapp,
                        'tomcat.util.scan.StandardJarScanFilter.jarsToSkip': '*.jar']
}
