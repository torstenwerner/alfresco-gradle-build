configurations {
    war
}

dependencies {
    compile project(':tomcat-embedded-launcher'),
            project(':alfresco-extension')

    runtime 'com.h2database:h2:1.4.192',
            files("$buildDir/WEB-INF/classes"),
            fileTree(dir: "$buildDir/WEB-INF/lib", include: '*.jar')
    runtime("org.alfresco:alfresco-repository:$alfrescoVersion:h2scripts") {
        transitive = false
    }

    war "org.alfresco:alfresco:$alfrescoVersion:@war"

    testCompile 'junit:junit:4.12',
            "org.springframework:spring-test:$springVersion",
            "org.springframework:spring-web:$springVersion"
}

def webapp = "$buildDir/webapps/alfresco"

task extractWar(type: Copy) {
    from {
        configurations.war.collect { zipTree(it) }
    }
    into webapp
    eachFile { details ->
        details.path = details.path.replaceFirst('WEB-INF/classes', '../../WEB-INF/classes')
        details.path = details.path.replaceFirst('WEB-INF/lib', '../../WEB-INF/lib')
    }
}

def tomcatProperties = ['tomcat.port'                                      : '8080',
                        'tomcat.context-path'                              : '/alfresco',
                        'tomcat.doc-base'                                  : webapp,
                        'tomcat.util.scan.StandardJarScanFilter.jarsToSkip': '*.jar']

task startTomcat(type: JavaExec, dependsOn: extractWar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'xyz.its_me.TomcatLauncher'
    systemProperties = tomcatProperties
}

test {
    dependsOn extractWar
    systemProperties = tomcatProperties
}
