group 'xyz.its_me'
version '0.1-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url 'https://artifacts.alfresco.com/nexus/content/groups/public/'
    }
}

configurations {
    alfresco
}

dependencies {
    alfresco("org.alfresco:alfresco:$alfrescoVersion:@war") {
        transitive = false
    }

    testCompile 'junit:junit:4.12',
            "org.springframework:spring-context:$springVersion",
            "org.alfresco:alfresco-data-model:$alfrescoVersion"

    testRuntime 'mysql:mysql-connector-java:5.1.39'
}

def alfrescoPath = "$buildDir/alfresco"

task extractAlfresco(type: Copy) {
    from {
        configurations.alfresco.collect { zipTree(it) }
    }
    into alfrescoPath
}

test {
    classpath = sourceSets.test.runtimeClasspath + files("$alfrescoPath/WEB-INF/classes") +
            fileTree(dir: "$alfrescoPath/WEB-INF/lib", include: '*.jar')
    dependsOn extractAlfresco
}

